buildscript {
    repositories {
        maven {
            name 'JFrog OSS snapshot repo'
            url  'https://oss.jfrog.org/oss-snapshot-local/'
        }
        jcenter()
    }
 
    dependencies {
        classpath 'me.champeau.gradle:antlr4-gradle-plugin:0.1.1-SNAPSHOT'
    }
}
 
repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'java'
apply plugin: 'me.champeau.gradle.antlr4'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.github.almondbranch.command-line-parser"
archivesBaseName = "command-line-parser"
version = "1.0.0"

dependencies {
    compile 'org.antlr:antlr4:4.7'
    testCompile "junit:junit:4.12", "org.junit.jupiter:junit-jupiter-api:5.1.0-M1"
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.3.1'
}

antlr4 {
    source = file('src/main/antlr4')
    output = file('build/generated-src/parser')
    extraArgs = ['-visitor', '-package', 'com.github.almondbranch.command_line_parser']
}
 
compileJava.dependsOn antlr4
 
sourceSets.main.java.srcDirs += antlr4.output
 
configurations {
    compile.extendsFrom antlr4
}

test {
    testLogging.showStandardStreams = true
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

//Add dummy values for the follow two parameters so that the automated build won't complain about them not existing
ossrhUsername=willBeReplacedWithValueFromGradleProperties
ossrhPassword=willBeReplacedWithValueFromGradleProperties

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'command-line-parser'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'Allows a direct call to a main(String[] args) method using an unparsed string containing the arguments.'
        url 'https://github.com/AlmondBranch/command-line-parser'

        scm {
          connection 'scm:git:git://github.com/AlmondBranch/command-line-parser.git'
          developerConnection 'scm:git:ssh://github.com/AlmondBranch/command-line-parser.git'
          url 'https://github.com/AlmondBranch/command-line-parser.git'
        }

        licenses {
          license {
            name 'MIT License'
            url 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            name 'Scott Haney'
            organization 'AlmondBranch'
            email 'emailalmondbranch@gmail.com'
          }
        }
      }
    }
  }
}


